Use DataDict.pkg

Open OrderDtl
Open OrderHea
Open Invt

Class cOrderDtlDataDictionary is a DataDictionary
    
    Procedure Construct_Object
        Forward Send Construct_Object
        Set Main_File to OrderDtl.File_Number

        Set Add_Server_File to OrderHea.File_Number
        Set Add_Server_File to Invt.File_Number

        Set Foreign_Field_Option DD_KEYFIELD DD_NOPUT to True
        Set Foreign_Field_Option DD_KEYFIELD DD_FINDREQ to True
        Set Foreign_Field_Option DD_INDEXFIELD DD_NOPUT to True
        Set Foreign_Field_Option DD_DEFAULT DD_DISPLAYONLY to True

        Set Field_Option Field OrderDtl.Order_Number DD_NOPUT to True

        Set Field_Option Field OrderDtl.Detail_Number DD_NOPUT to True
        Set Field_Auto_Increment Field OrderDtl.Detail_Number to File_Field OrderHea.Last_Detail_Num

        Set Field_Mask_Type Field OrderDtl.Qty_Ordered to Mask_Currency_Window
        Set Field_Label_Long Field OrderDtl.Qty_Ordered to "Quantity Ordered"
        Set Field_Label_Short Field OrderDtl.Qty_Ordered to "Quantity"
        Set Status_Help Field OrderDtl.Qty_Ordered to "Number of items ordered"
        Set Field_Exit_msg Field OrderDtl.Qty_Ordered to Adjust_Display_Total

        Set Field_Label_Long Field OrderDtl.Price to "Price per Unit"
        Set Field_Label_Short Field OrderDtl.Price to "Price"
        Set Status_Help Field OrderDtl.Price to "Price per Unit"
        Set Field_Entry_msg Field OrderDtl.Price to Entering_Price

        Set Field_Option Field OrderDtl.Extended_Price DD_DISPLAYONLY to True
        Set Field_Label_Long Field OrderDtl.Extended_Price to "Extended Price"
        Set Field_Label_Short Field OrderDtl.Extended_Price to "Total"
        Set Status_Help Field OrderDtl.Extended_Price to "Total extended price"

    End_Procedure
    
    Procedure Adjust_Balances Number Qty Number Amt
        Subtract Qty from Invt.On_Hand
        Add Amt to Orderhea.Order_Total
    End_Procedure

    Procedure Backout
        Forward Send Backout
        Send Adjust_Balances (-OrderDtl.Qty_Ordered) (-OrderDtl.Extended_Price)
    End_Procedure

    Procedure Update
        Forward Send Update
        Move (OrderDtl.Price * OrderDtl.Qty_Ordered) to OrderDtl.Extended_Price
        Send Adjust_Balances OrderDtl.Qty_Ordered OrderDtl.Extended_Price
    End_Procedure
    
    Procedure Adjust_Display_Total Integer iField Number nAmnt

        Integer iQty
        Get Field_Current_Value Field Orderdtl.Qty_Ordered to iQty
        Get Field_Current_Value Field Orderdtl.Price to nAmnt
        Set Field_Current_Value Field Orderdtl.Extended_Price to (nAmnt * iQty)
        // note we set value, but not changed state!
    End_Procedure

    Procedure Entering_Price Integer Field# Number nAmnt
        Handle hoInvtDD
        Boolean bHasRecord
        
        Get Data_Set Invt.File_Number to hoInvtDD
        If (hoInvtDD) Begin
            Get HasRecord of hoInvtDD to bHasRecord
            
        If (nAmnt=0 and bHasRecord) Begin
            Get File_Field_Current_Value File_Field Invt.Unit_Price to nAmnt
            Set Field_Changed_Value Field# to nAmnt
            Send Adjust_Display_Total Field# nAmnt
           End
        End

    End_Procedure

End_Class